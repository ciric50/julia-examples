{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "21995280",
   "metadata": {},
   "source": [
    "# Trefethen Problem 2\n",
    "February 25, 2023\n",
    "\n",
    "> A photon moving at speed 1 in the x-y plane starts at t = 0 at (x, y) = (1/2, 1/10)\n",
    "heading due east. Around every integer lattice point (i, j) in the plane, a circular\n",
    "mirror of radius 1/3 has been erected. How far from the origin is the photon at\n",
    "t = 10?\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "46438296",
   "metadata": {},
   "source": [
    "## Notes:\n",
    "\n",
    "1. The problem has chosen units so that the speed of light is 1.\n",
    "1. Clearly if the photon is completely between the rows or columns of mirrors and moving parallel to them, the photon will go on forever without hitting any mirror. However it cannot get to any of these paths by bouncing off of a mirror.\n",
    "1. What happens if the photon happens to be travelling so that it becomes exactly tangent to one of the mirrors? If you think about this in the limit as the angle of the incident ray changes, clearly the ray would continue straight as if it didn't hit the mirror.\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "1d3ba8c9",
   "metadata": {},
   "source": [
    "## Finding collision points\n",
    "\n",
    "Finding where the photon collides with a mirror is equivalent to finding the points of intersection between a line and a circle. We can start by writing the slope-intercept equation for a line\n",
    "$$\n",
    "y = mx+b.\n",
    "$$\n",
    "\n",
    "The equation of a circle with center at $x=h, y=k$ and radius $r$ is given by\n",
    "\n",
    "$$\n",
    "(x-h)^2 + (y-k)^2 = r^2.\n",
    "$$\n",
    "\n",
    "We can substitue the value of $y$ from the line equation into the circle equation to get an equation with only $x$ as the unknown, namely\n",
    "\n",
    "$$\n",
    "(mx-k+b)^2 + (x-h)^2 = r^2\n",
    "$$\n",
    "\n",
    "which can be solved for $x$:\n",
    "\n",
    "$$\n",
    "x=\\frac{\\pm\\sqrt{\\left( {{m}^{2}}+1\\right) \\, {{r}^{2}}-{{h}^{2}}\\, {{m}^{2}}+\\left( 2 h k-2 b h\\right)  m-{{k}^{2}}+2 b k-{{b}^{2}}}+\\left( k-b\\right)  m+h}{{{m}^{2}}+1}\n",
    "$$\n",
    "\n",
    "However, the line for the photon's trajectory is not given in slope-intercept form. In practice we know the position of the photon $(p_x, p_y)$ and a 2D vector for its velocity $\\vec{v} = v_x \\hat{i} + v_y \\hat{j}$. From this information we can find the slope $m$ and intercept $b$ from\n",
    "$$\n",
    "\\begin{align}\n",
    "m &= \\frac{v_y}{v_x} \\\\\n",
    "b &= p_y - \\frac{p_x v_y}{v_x}\n",
    "\\end{align}\n",
    "$$\n",
    "and substitute these values into the solution above.\n",
    "\n",
    "There is still the problem of how we handle the situation where $v_x=0$, since we then have an infinite slope with a vertical trajectory and the solution equation blows up. However in this case the equation of the line is just\n",
    "\n",
    "$$\n",
    "x = p_x\n",
    "$$\n",
    "\n",
    "and we can substitute this into the circle equation to get a solution for $y$:\n",
    "\n",
    "$$\n",
    "y=k \\pm \\sqrt{r^2 - p_x^{2} +2h p_x - h^2}.\n",
    "$$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "fbf0498b",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "intersections (generic function with 1 method)"
      ]
     },
     "execution_count": 1,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function intersections(center::Vector{Int64}, r::BigFloat, p::Vector{BigFloat}, v::Vector{BigFloat})\n",
    "    #=\n",
    "    Finds the intersection points of a line with a circle, with the line\n",
    "    specified by a vector v originating from a point p.\n",
    "    \n",
    "    Arguments:\n",
    "        center: 2D vector for the center of the circle\n",
    "        r: radius of the circle\n",
    "        p: 2D vector for the position of the photon\n",
    "        v: 2D vector for the velocity of the photon\n",
    "\n",
    "    Returns: a 2-tuple\n",
    "        If there are 2 intersection points, they are returned as\n",
    "            (Vector{BigFloat}, Vector{BigFloat})\n",
    "        If there is just one intersection point it is returned as\n",
    "        the first tuple value, with nothing in the second value\n",
    "            (Vector{BigFloat}, nothing)\n",
    "        If there are no intersection points this function returns\n",
    "        both tuple values as nothing\n",
    "            (nothing, nothing)\n",
    "    =#\n",
    "    ret = (nothing, nothing)\n",
    "    h = BigFloat(center[1])\n",
    "    k = BigFloat(center[2])\n",
    "    \n",
    "    # Check for a vertical line\n",
    "    if v[1] == 0\n",
    "        # The trajectory is vertical\n",
    "        disc = r^2 - p[1]^2 + 2h*p[1] - h^2\n",
    "        if disc == 0\n",
    "            ret = ([p[1],k], nothing)\n",
    "        elseif disc > 0\n",
    "            sq = sqrt(disc)\n",
    "            ret = ([p[1],k+sq], [p[1],k-sq])\n",
    "        end\n",
    "    else\n",
    "        # The trajectory is not vertical\n",
    "        m = v[2] / v[1]\n",
    "        b = p[2] - m*p[1]\n",
    "        \n",
    "        disc = (m^2 + 1)*r^2 - (h*m)^2 + 2h*m*(k-b) - k^2 + 2b*k - b^2\n",
    "        if disc == 0\n",
    "            #println(\"One solution\")\n",
    "            x1 = ((k - b)*m + h) / (m^2 + 1)\n",
    "            y1 = m*x1 + b\n",
    "            ret = ([x1,y1], nothing)\n",
    "        elseif disc > 0\n",
    "            #println(\"Two solutions\")\n",
    "            sq = sqrt(disc)\n",
    "            x1 = ((k - b)*m + h + sq) / (m^2 + 1)\n",
    "            y1 = m*x1 + b\n",
    "            x2 = ((k - b)*m + h - sq) / (m^2 + 1)\n",
    "            y2 = m*x2 + b\n",
    "            ret = ([x1,y1], [x2,y2])\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    return ret\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "b5b9a1df",
   "metadata": {},
   "source": [
    "The function below determines whether a photon in a given position is traveling in a direction such that it will collide with a circle. Using the function **intersections** above it can be determined whether the line the photon is following intersects the circle. However, if that line does intersect the circle, we still have two cases to consider:\n",
    "1. The photon is traveling away from the intersection point, so it never collides with the circle.\n",
    "1. The photon is traveling towards the intersection point, and so *will* collide with the circle.\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "f97ce52f",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "photon_intersect (generic function with 1 method)"
      ]
     },
     "execution_count": 2,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function photon_intersect(center::Vector{Int64}, r::BigFloat, p::Vector{BigFloat}, v::Vector{BigFloat})\n",
    "    #=\n",
    "    Determines the collision point between a photon and a circular mirror, if any.\n",
    "\n",
    "    Arguments:\n",
    "        center: 2D vector for the center of the circle\n",
    "        r: radius of the circle\n",
    "        p: 2D vector for the position of the photon\n",
    "        v: 2D vector for the velocity of the photon\n",
    "\n",
    "    Returns: nothing if there is no collision point, else\n",
    "             returns the coordinates of the collision point\n",
    "    =#\n",
    "    bigzero = BigFloat(\"0\")\n",
    "    retval = nothing\n",
    "    coll1, coll2 = intersections(center, r, p, v)\n",
    "    if coll1 == nothing\n",
    "        return nothing  # No collision point\n",
    "    elseif coll2 == nothing\n",
    "        # Form the vector from the photon position to the collision point\n",
    "        pc = coll1 .- p\n",
    "\n",
    "        # Take the inner product with the velocity vector\n",
    "        inner_prod = sum(pc .* v)\n",
    "\n",
    "        if inner_prod > 0.0\n",
    "            retval = coll1\n",
    "        end\n",
    "    else\n",
    "        # Two intersection points - determine if one or both will\n",
    "        # be reached by the photon\n",
    "        pc1 = coll1 .- p\n",
    "        inner_prod1 = sum(pc1 .* v)\n",
    "        reached1 = inner_prod1 > bigzero\n",
    "        \n",
    "        pc2 = coll2 .- p\n",
    "        inner_prod2 = sum(pc2 .* v)\n",
    "        reached2 = inner_prod2 > bigzero\n",
    "        \n",
    "        if reached1\n",
    "            if reached2\n",
    "                # Both collision points are reached. Find which is closest.\n",
    "                if inner_prod1 < inner_prod2\n",
    "                    retval = coll1\n",
    "                else\n",
    "                    retval = coll2\n",
    "                end\n",
    "            else\n",
    "                retval = coll1\n",
    "            end\n",
    "        elseif reached2\n",
    "            retval = coll2\n",
    "        end\n",
    "    end\n",
    "\n",
    "    return retval\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e9569f53",
   "metadata": {},
   "source": [
    "## Calculating the new velocity vector\n",
    "\n",
    "From http://www.paulbourke.net/geometry/reflected/ the formula for the reflected velocity vector is\n",
    "\n",
    "$$\n",
    "{\\bf v}_r = {\\bf v}_i - 2 {\\bf N} ({\\bf v}_i \\cdot {\\bf N})\n",
    "$$\n",
    "where\n",
    "- ${\\bf v}_r$ is the reflected velocity vector\n",
    "- ${\\bf v}_i$ is the incident velocity vector\n",
    "- ${\\bf N}$ is the normal vector at the collision point"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "e17e4453",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "new_velocity (generic function with 1 method)"
      ]
     },
     "execution_count": 3,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function new_velocity(center::Vector{Int64}, r::BigFloat, v_i::Vector{BigFloat}, coll::Vector{BigFloat})\n",
    "    #=\n",
    "    Determine the new velocity of a photon after a collision.\n",
    "    \n",
    "    Arguments:\n",
    "        center: 2D vector for the center of the circle\n",
    "        r: radius of the circle\n",
    "        v_i: 2D vector for the incident velocity of the photon\n",
    "        coll: 2D vector for the collision point\n",
    "    =#\n",
    "\n",
    "    # Calculate the unit normal at the collision point\n",
    "    fcenter = (BigFloat.(center))\n",
    "    nv = (coll .- fcenter)\n",
    "    N = nv ./ sqrt(sum(nv.^2))\n",
    "    #println(\"N = $N\")\n",
    "\n",
    "    # Compute the component of the incident velocity along the normal\n",
    "    vi_N = sum(v_i .* N)\n",
    "\n",
    "    # Compute the reflected velocity\n",
    "    v_r = v_i .- (BigFloat(\"2\") * vi_N) .* N\n",
    "    return v_r\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "eb5b8d55",
   "metadata": {},
   "source": [
    "A quick sketch of the problem reveals that the first collision will occur with the mirror centered at $(1,0)$. From that point forward the problem is to determine the next circle the ray will collide with. The geometry of the problem is such that the ray cannot strike the same mirror immediately because the surface is convex. We will have the components of the velocity vector after the collision, which will allow us to limit the search to circles in a quadrant around the current circle. For example, if the velocity following the first collision had a positive $y$ component and a negative $x$ component, can immediately say that the second collision will be with a circle having a center $(x_2, y_2)$ where $x_2 \\le 1$ and $y_2 \\ge 0$.\n",
    "\n",
    "Actually we can go even further than that. From the $(1,0)$ cell, the only cells that can be reached by the ray are\n",
    "- Adjacent cells $(0,1)$, $(0,0)$, $(1,1)$\n",
    "- $(0,j)$ for $j\\ge 1$\n",
    "- $(i,1)$ for $i\\le -1$.\n",
    "\n",
    "Additionally we can look at the relative sizes of the $x$ and $y$ components of the velocity to limit the choices even more. For example, if $v_x > 0, v_y > 0$, and $v_x < v_y$, the only cells possible from cell $(i,j)$ are\n",
    "- Adjacent cell just above at $(i,j+1)$\n",
    "- Cells $(i+1,k)$ for $k\\gt j$.\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "5c6e32b4",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "run (generic function with 1 method)"
      ]
     },
     "execution_count": 4,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function run(final_t::BigFloat)\n",
    "    # Set numerical precision\n",
    "    setprecision(BigFloat, 256)\n",
    "\n",
    "    # Initialize the trajectory\n",
    "    total_t = BigFloat(\"0\")\n",
    "    center = [1,0]\n",
    "    r = BigFloat(\"1.0\")/BigFloat(\"3\")\n",
    "    p = [BigFloat(\"0.5\"), BigFloat(\"0.1\")]\n",
    "    v = [BigFloat(\"1\"), BigFloat(\"0\")]\n",
    "\n",
    "    # Find first collision point and new velocity\n",
    "    coll = photon_intersect(center, r, p, v)\n",
    "    v = new_velocity(center, r, v, coll)\n",
    "\n",
    "    # Compute total time (distance) taken\n",
    "    displacement = coll .- p\n",
    "    total_t += sqrt(sum(displacement.^2))\n",
    "    center = [1,0]\n",
    "\n",
    "    cell = copy(center)\n",
    "    done = false\n",
    "    while !done\n",
    "        # center - holds the cell where collision occurred\n",
    "        # cell - holds cell we are testing for next collision\n",
    "        center = copy(cell)\n",
    "        p = coll\n",
    "\n",
    "        # Determine up/down and left/right\n",
    "        if v[2] > 0.0\n",
    "            Δy = 1   # Going up\n",
    "        else\n",
    "            Δy = -1  # Going down\n",
    "        end\n",
    "\n",
    "        if v[1] >= 0.0\n",
    "            Δx = 1   # Going right\n",
    "        else\n",
    "            Δx = -1  # Going left\n",
    "        end\n",
    "\n",
    "        # Determine if slope of v is more vertical or horizontal\n",
    "        vertical = (abs(v[1]) <= abs(v[2]))\n",
    "\n",
    "        # Check adjacent cell for collision\n",
    "        if vertical\n",
    "            cell = [center[1], center[2]+Δy]\n",
    "        else\n",
    "            cell = [center[1]+Δx, center[2]]\n",
    "        end\n",
    "        coll = photon_intersect(cell, r, p, v)\n",
    "\n",
    "        if coll == nothing\n",
    "            # Check corner cell\n",
    "            cx = center[1] + Δx\n",
    "            cy = center[2] + Δy\n",
    "            cell = [cx,cy]\n",
    "            coll = photon_intersect(cell, r, p, v)\n",
    "\n",
    "            if coll == nothing\n",
    "                # Check other possible cells\n",
    "                if vertical\n",
    "                    for k ∈ 1:9  # Max cells to check\n",
    "                        cy += Δy\n",
    "                        cell = [cx,cy]\n",
    "                        \n",
    "                        coll = photon_intersect(cell, r, p, v)\n",
    "                        if coll != nothing\n",
    "                            break\n",
    "                        end\n",
    "                    end\n",
    "                else\n",
    "                    # Horizontal\n",
    "                    for k ∈ 1:9  # Max cells to check\n",
    "                        cx += Δx\n",
    "                        cell = [cx,cy]\n",
    "                        \n",
    "                        coll = photon_intersect(cell, r, p, v)\n",
    "                        if coll != nothing\n",
    "                            break\n",
    "                        end\n",
    "                    end\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "\n",
    "        if coll == nothing\n",
    "            println(\"No collision found - aborting at t=$total_t\")\n",
    "            println(\"  p = $p\")\n",
    "            println(\"  v = $v\")\n",
    "            done = true\n",
    "        else\n",
    "            displacement = coll .- p\n",
    "            δxy = sqrt(sum(displacement.^2))\n",
    "            tmp = total_t + δxy\n",
    "            if tmp < final_t\n",
    "                total_t = tmp\n",
    "                v = new_velocity(cell, r, v, coll)\n",
    "            else\n",
    "                # Reached endpoint before collision\n",
    "                t = final_t - total_t\n",
    "                p[1] += v[1]*t\n",
    "                p[2] += v[2]*t\n",
    "                println(\"Final point is $p\")\n",
    "                dist = sqrt(p[1]^2 + p[2]^2)\n",
    "                println(\"Distance from origin = $dist\")\n",
    "                done = true\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "0dab3195",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Final point is BigFloat[-0.7362926986096183107757371718663917204515754910310241180155703213582650853773168, -0.669642696363571375194376070967098466862227372928775421597411093812897756001969]\n",
      "Distance from origin = 0.9952629194433541608903118094267216210294669227341543498032088580730132317121767\n"
     ]
    }
   ],
   "source": [
    "run(BigFloat(\"10\"))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "8bb3774e",
   "metadata": {},
   "source": [
    "The solution above has been verified by comparison with the solution written up by F. H. Simons."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e8b1fe4f",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.8.4",
   "language": "julia",
   "name": "julia-1.8"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.8.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
